buildscript {
    ext.kotlin_version = '1.1.4-3'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

group 'com.chimerapps.gradle'
version '1.0.0'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7
targetCompatibility = 1.7

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'docs/README.txt'
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.moshi:moshi:1.5.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.3.0'

    compile 'com.chimerapps:moshi-generator-runtime:0.5.9'
    kapt 'com.chimerapps:moshi-generator:0.5.9'

    testCompile 'junit:junit:4.12'
}

sourceSets {
    main {
        java {
            srcDir "${buildDir.absolutePath}/generated/source/kapt/main"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'transifex-gradle-plugin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/transifex-android-gradle-plugin'

        version {
            name = "$version"
            desc = "Gradle plugin for transifex"
            released = new Date()
        }
    }
    publications = ['MyPublication']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            groupId 'com.chimerapps.gradle'
            artifactId 'transifex-plugin'
            version '1.0.0'

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin that allows translations to be downloaded from transifex')
                root.appendNode('name', 'transifex-gradle-plugin')
                root.appendNode('url', 'https://github.com/Chimerapps/moshi-generator')
                root.children().last() + pomConfig
            }
        }
    }
}
